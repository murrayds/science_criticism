from os.path import join as j

# Use a config file
configfile: "config.yaml"

RAW_DIR = j(config["data_dir"], "raw")
DERIVED_DIR = j(config["data_dir"], "derived")

FIG_DIR = j(config["fig_dir"], "figs")

LETTER_METADATA = j(RAW_DIR, "{venue}_letter_metadata.csv")
PAPER_IMPACT = j(RAW_DIR, "{venue}_paper_impact.csv")
PAPER_FIELDS = j(RAW_DIR, "{venue}_fields.csv")
PAPER_RETRACTIONS = j(RAW_DIR, "{venue}_retractions.csv")
FIELD_HIERARCHY = j(RAW_DIR, "mag_field_hierarchy.csv")
#TODO Later this should be integrated in files at query-time
MONTH_OF_PUBLICATION = j(RAW_DIR, "month_of_publication.csv")

AGG_LETTERS = j(DERIVED_DIR, "agg", "agg_letters.csv")
AGG_NONLETTERS = j(DERIVED_DIR, "agg", "agg_nonletters.csv")
AGG_FIELDS = j(DERIVED_DIR, "agg", "agg_fields.csv")

MATCHED_PAPERS_FOR_IMPACT_COMPARISON = j(DERIVED_DIR, "matched", "matched_papers_stage1_{delay}delay_{cite_tolerance}impact_{year_tolerance}year.csv")

IMPACT_RANK_HISTOGRAM = j(FIG_DIR, "impact_rank_histogram.png")
IMPACT_LIKELIHOOD_SCATTER = j(FIG_DIR, "impact_likelihood_scatter.png")

FIT_BY_VENUE_TABLE = j(FIG_DIR, "tables", "fit_by_venue.txt")


POOLED_IMPACT_COMPARISON_PLOT = j(FIG_DIR, "pooled_impact_comparison_plot.png")
PAIRWISE_IMPACT_COMPARISON_PLOT = j(FIG_DIR, "pairwise_impact_comparison_plot.png")
IMPACT_COMPARISON_TEST_TABLE = j(FIG_DIR, "tables", "impact_comparison_table.txt")

TABLE_MATCH_DIAGNOSTICS = j(FIG_DIR, "tables", "match_diagnostics.csv")
MATCHING_DIAGNOSTIC_IMPACT_PLOT = j(FIG_DIR, "diagnostics", "matching_diagnostic_impact_plot.png")


####################
# PARAMETERS
####################
VENUES = ["Nature", "Science", "PNAS", "PRL"]

# Maybe I can store these in a config file instead...?
IMPACT_DELAY = [2, 3, 4]
CITE_TOLERANCE = [0.05, 0.1, 0.15]
YEAR_TOLERANCE = [1, 2, 3]

rule all:
    input:
        IMPACT_RANK_HISTOGRAM,
        IMPACT_LIKELIHOOD_SCATTER,
        expand(
            MATCHED_PAPERS_FOR_IMPACT_COMPARISON,
            delay = IMPACT_DELAY,
            cite_tolerance = CITE_TOLERANCE,
            year_tolerance = YEAR_TOLERANCE
        ),
        POOLED_IMPACT_COMPARISON_PLOT,
        PAIRWISE_IMPACT_COMPARISON_PLOT,
        FIT_BY_VENUE_TABLE,
        TABLE_MATCH_DIAGNOSTICS,
        MATCHING_DIAGNOSTIC_IMPACT_PLOT

rule agg_letters:
    input:
        letters=expand(LETTER_METADATA, venue=VENUES),
        impacts=expand(PAPER_IMPACT, venue=VENUES),
        month=MONTH_OF_PUBLICATION
    output: AGG_LETTERS
    script: "scripts/processing/agg_letters.py"

rule agg_nonletters:
    input: 
        letters=expand(LETTER_METADATA, venue=VENUES),
        impacts=expand(PAPER_IMPACT, venue=VENUES),
        retractions=expand(PAPER_RETRACTIONS, venue=VENUES),
        month=MONTH_OF_PUBLICATION
    output: AGG_NONLETTERS
    script: "scripts/processing/agg_nonletters.py"
        
rule agg_fields:
    input:
        fields = expand(PAPER_FIELDS, venue=VENUES),
    params: FIELD_HIERARCHY
    output: AGG_FIELDS
    script: "scripts/processing/agg_fields.py"

# so what wildcards are there...
    # Impact_before tolerance, 0.05 or 0.1 or 0.025?
    # Year, 1, 2, or 3
    # Minimum before citations...
rule match_papers_for_impact_comparison:
    input: 
        rules.agg_letters.output,
        rules.agg_nonletters.output,
        rules.agg_fields.output
    output: MATCHED_PAPERS_FOR_IMPACT_COMPARISON
    script: "scripts/processing/match_papers_for_impact_comparison.R"

rule plot_impact_rank_histogram:
    input:
        rules.agg_letters.output,
        rules.agg_nonletters.output,
        rules.agg_fields.output
    output: IMPACT_RANK_HISTOGRAM
    script: "scripts/plotting/plot_impact_rank_histogram.R"

rule plot_impact_likelihood_scatter:
    input:
        rules.agg_letters.output,
        rules.agg_nonletters.output
    output: IMPACT_LIKELIHOOD_SCATTER
    script: "scripts/plotting/plot_impact_likelihood_scatter.R"

rule table_fit_by_venue:
    input: 
        rules.agg_letters.output,
        rules.agg_nonletters.output,
        rules.agg_fields.output
    output: FIT_BY_VENUE_TABLE
    script: "scripts/tables/table_fit_by_venue.R"

rule table_match_diagnostics:
    input: 
        rules.agg_letters.output,
        expand(
            rules.match_papers_for_impact_comparison.output,
            delay = IMPACT_DELAY,
            cite_tolerance = CITE_TOLERANCE,
            year_tolerance = YEAR_TOLERANCE
        )
    output: TABLE_MATCH_DIAGNOSTICS
    script: "scripts/tables/table_match_diagnostics.R"

rule plot_impact_comparison:
    input: 
        expand(
            rules.match_papers_for_impact_comparison.output,
            delay = 3,
            cite_tolerance = 0.10,
            year_tolerance = 1
        )
    output:
        POOLED_IMPACT_COMPARISON_PLOT,
        PAIRWISE_IMPACT_COMPARISON_PLOT,
        IMPACT_COMPARISON_TEST_TABLE
    script: "scripts/plotting/plot_impact_comparison.R"

rule plot_match_diagnostic_impact:
    input:
        expand(
            rules.match_papers_for_impact_comparison.output,
            delay = 3,
            cite_tolerance = 0.10,
            year_tolerance = 1
        )
    output:
        MATCHING_DIAGNOSTIC_IMPACT_PLOT
    script: "scripts/plotting/plot_match_diagnostic_impact.R"